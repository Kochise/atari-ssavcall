; /// STarter By KOSS / 1996 - 1998 ///
;
; Le meilleur STarter de programme jamais ‚crit.
; Sans modestie aucune, j'avoue avoir fait trŠs fort trŠs facilement :
; Ici, … la fin du STarter, le programmeur est incapable de faire
; la diff‚rence entre avant et aprŠs (mˆme le pointeur de la
; BasePage est report‚). C'est dire la transparence...
;
; Used registers : D7.l - Progiz' Size
;                  D2.l > TrashCan
;                  D1.l > TrashCan
;                  D0.l > TrashCan
;                  A0.l > TrashCan
;                  A6.l > TrashCan
;
; Les registres avec l'attributs "> TrashCan" sont des registres non importants
; qui ne servent que de fa‡on locale.
; Les autres, commencant par "- " doivent IMPERATIVEMENT ne pas etre chang‚s
; car seront utilis‚s par la suite dans les autres includes

	movea.l	(4,SP),A6	; Adresse de la BASEPAGE
	move.l	($C,A6),D0	; Taille du SEGMENT TEXT
	add.l	($14,A6),D0	; Additionne la taille du SEGMENT DATA … D0
	add.l	($1C,A6),D0	; Additionne la taille du SEGMENT BSS … D0
	addi.l	#$100,D0	; Additionne la taille de la BASEPAGE … D0
	addi.l	#$400,D0	; Additionne la taille de la future PILE (Ici, 256 positions longues (1024 OCTETS))
	move.l	D0,D1	; Copie D0 dans D1 (Taille … r‚server)
	move.l	D0,D7
	add.l	A6,D1	; Additionne l'adresse du d‚but du programme avec D1 (La taille … r‚server)
	andi.l	#-2,D1	; Rend D1 pair (Au cas o— il ne l'‚tait pas)
	subq.l	#8,D1	; Garde de la place pour l'adresse de la BASEPAGE
	move.l	(4,SP),D2	; Sauvegarde l'adresse de la BASEPAGE
	movea.l	D1,SP	; Installe l'adresse de fin du programme+place nouvelle PILE dans la PILE
	move.l	D2,(4,SP)	; R‚installe l'adresse de la BASEPAGE
	move.l	D0,-(SP)	; EmPILE la taille … r‚server
	move.l	A6,-(SP)	; EmPILE l'adresse du programme
	clr.w	-(SP)	; R‚serv‚
	move.w	#$4A,-(SP)	; - M_SHRINK -
	trap	#1	; *** GEMDOS ***
	lea	(12,SP),SP

