; /// SSAV AutoInstaller v1.63 ///

	movea.l	4(SP),A6		; Adresse de la BASEPAGE
	move.l	$C(A6),D0		; Taille du SEGMENT TEXT
	add.l	$14(A6),D0		; Additionne la taille du SEGMENT DATA … D0
	add.l	$1C(A6),D0		; Additionne la taille du SEGMENT BSS … D0
	addi.l	#$100,D0		; Additionne la taille de la BASEPAGE … D0
	addi.l	#$400,D0		; Additionne la taille de la future PILE (Ici, 256 positions longues (1024 OCTETS))
	move.l	D0,D1		; Copie D0 dans D1 (Taille … r‚server)
	move.l	D0,D7
	add.l	A6,D1		; Additionne l'adresse du d‚but du programme avec D1 (La taille … r‚server)
	andi.l	#-2,D1		; Rend D1 pair (Au cas o— il ne l'‚tait pas)
	movea.l	D1,SP		; Installe l'adresse de fin du programme+place nouvelle PILE dans la PILE
	move.l	D0,-(SP)		; EmPILE la taille … reserver	\
	move.l	A6,-(SP)		; EmPILE l'adresse du programme |-> Rend le reste de la m‚moire au systŠme
	clr.w	-(SP)		; R‚serv‚                       |
	move.w	#$4A,-(SP)		; - M_SHRINK -                  |
	trap	#1		; *** GEMDOS ***                /
	lea	12(SP),SP
	tst	D0		; Si D0<>0, problŠme grave
	BNE	P_Term
	
	pea	Cookie_Hacker
	move.w	#$26,-(SP)		; - SupExec -
	trap	#14		; *** XBIOS ***
	addq.l	#6,SP

	tst.l	D0
	BEQ	OK_Install
	
	cmpi.l	#-1,D0
	BEQ	NO_Install
	
	cmpi.l	#-2,D0
	BEQ	AR_Install	
	
	
P_TermRes	clr.w	-(SP)
	move.l	D7,-(SP)
	move.w	#$31,-(SP)		; - P_TermRes -
	trap	#1		; *** GEMDOS ***
	addq.l	#8,SP
	
P_Term	move.w	D0,-(SP)
	move.w	#$4C,-(SP)		; - P_Term -
	trap	#1		; *** GEMDOS ***
	

; *** L'installeur ***

Cookie_Hacker	move.l	$5A0.w,D0		; L'adresse de la Cookie-Jar
	BEQ	KO_Install
	
	movea.l	D0,A0
	move.l	D0,D1

	cmpi.l	#"NULL",(A0)		; Y'a p'tet queq'chose
	BEQ	Go_For_SSAV
	
Look_For_Null	move.l	(A0,D3*8),D0
	BEQ	Next_Null
	
	addq.l	#1,D3		; Le compteur de Cookies
	BRA	Look_For_Null
	
Next_Null	move.l	(4,A0,D3*8),D2		; Le nombre de Cookies
	cmp.l	D2,D3
	BGE	KO_Install

Move_Cookies	move.l	(-8,A0,D3*8),(A0,D3*8)
	move.l	(-4,A0,D3*8),(4,A0,D3*8)
	subq.l	#1,D3
	BNE	Move_Cookies
	
	move.l	#"NULL",(A0)
	move.l	D2,(4,A0)
	
Go_For_SSAV	move.l	(4,A0),D3

	move.l	Cookie_Name,D1
		
Look_For_SSAV	cmp.l	D3,D4
	BGE	KO_Install
	move.l	(A0,D4*8),D0
	BEQ	Install_Rout
	cmp.l	D0,D1
	BEQ	AlReady_SSAV

	addq.l	#1,D4

	BRA	Look_For_SSAV

AlReady_SSAV	move.l	#-2,D0		; D‚j… install‚
	
	RTS			; On sort de l'installation

Install_Rout	move.l	D1,(A0,D4*8)
	move.l	#START_SSAV,(4,A0,D4*8)
	clr.l	(8,A0,D4*8)
	move.l	D2,(12,A0,D4*8)
	
	clr.l	D0		; Well Done...
	
	RTS
		
; *** Les routines graphiques ***
		
KO_Install	move.l	#-1,D0		; Y'a eu un n'os
	
	RTS
	
OK_Install
	
	BRA	P_TermRes

NO_Install

	BRA	P_Term
	
AR_Install

	BRA	P_Term
	